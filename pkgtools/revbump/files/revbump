#!@PERL@ -w
# -*- perl -*-
#
# $NetBSD: revbump,v 1.3 2006/03/09 17:24:48 wiz Exp $
#
# Copyright (c) 2003, 2004 The NetBSD Foundation, Inc.
#
# This code was originally contributed to the NetBSD Foundation, Inc.
# by Julio Merino <jmmv@NetBSD.org> and Thomas Klausner <wiz@NetBSD.org>.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in
#    the documentation and/or other materials provided with the
#    distribution.
# 3. Neither the name of author nor the names of its contributors may
#    be used to endorse or promote products derived from this software
#    without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE NETBSD FOUNDATION, INC. AND
# CONTRIBUTORS ``AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
# IN NO EVENT SHALL THE FOUNDATION OR CONTRIBUTORS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
# GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER
# IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
# OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
# IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#

use strict;
use Getopt::Std;

my (@PACKAGES, $PKGSRCDIR, %opt, $pkg);

$PKGSRCDIR = $ENV{PKGSRCDIR};
if (! $PKGSRCDIR) {
    $PKGSRCDIR = "/usr/pkgsrc";
}

sub usage {
    print STDERR "usage: revbump [-h] [-p pkgsrcdir] [cat1/pkg1 ... catN/pkgN]\n\n";
    print STDERR "If no packages are given as arguments, the list is read from stdin.\n";
    print STDERR "Packages for which revision cannot be bumped are written to stdout.\n";
    exit 0;
}

sub revbump {
    my $pkgdir = shift;
    my ($pkg, $oldpkgver, $pkgver, $done, $oldmk, $newmk, $backupmk);
    local (*OLDMK, *MAKE);

    $pkgdir =~ /\/(.*)$/;
    $pkg = $1;

    $oldmk = "$PKGSRCDIR/$pkgdir/Makefile";

    open(MAKE, '-|', "cd $PKGSRCDIR/$pkgdir ; make show-var VARNAME=PKGVERSION");
    $oldpkgver = <MAKE>;
    close(MAKE);
    chop $oldpkgver;

    open(OLDMK, $oldmk);
    $newmk = "$oldmk.new";
    open(NEWMK, ">$newmk");

    $done = 0;
    while (<OLDMK>) {
        if ($done == 0 && /^PKGREVISION=([ \t]+)([0-9]+)$/) {
            $done = $2 + 1;
            print NEWMK "PKGREVISION=${1}${done}\n";
        } elsif ($done == 0 && /^SVR4_PKGNAME=([ \t]+)/) {
            print NEWMK "PKGREVISION=${1}1\n";
            print NEWMK $_;
            $done = 1;
        } elsif ($done == 0 && /^CATEGORIES=([ \t]+)/) {
            print NEWMK "PKGREVISION=${1}1\n";
            print NEWMK $_;
            $done = 1;
        } else {
            print NEWMK $_;
        }
    }

    # no suitable place found
    if ($done == 0) {
        close(NEWMK);
        close(OLDMK);
        unlink "$newmk";
        return 0;
    }

    close(NEWMK);
    close(OLDMK);
    $backupmk = "$oldmk.orig";
    rename("$oldmk", "$backupmk");
    rename("$newmk", "$oldmk");

    open(MAKE, '-|', "cd $PKGSRCDIR/$pkgdir ; make show-var VARNAME=PKGVERSION");
    $pkgver = <MAKE>;
    close(MAKE);
    chop $pkgver;

    # compare previous PKGVERSION and current PKGVERSION to see if update
    # was successful
    if ($oldpkgver =~  /.*nb([0-9]+)$/) {
	$oldpkgver = $1;
    } else {
	$oldpkgver = 0;
    }
    if ($pkgver =~  /.*nb([0-9]+)$/) {
	$pkgver = $1;
    } else {
	$pkgver = 0;
    }
    if ($oldpkgver + 1 != $pkgver) {
	# unsuccessful, revert change
	rename("$backupmk", "$oldmk");
	return 0;
    }

    unlink "$backupmk";

    print("Updated $pkgdir from $oldpkgver to $pkgver\n");

    return 1;
}

getopts('hp:', \%opt);
usage() if $opt{h};
if ($opt{p}) {
    $PKGSRCDIR = $opt{p};
}

@PACKAGES = ();
push @PACKAGES, @ARGV;

if (! -d $PKGSRCDIR || ! -d "$PKGSRCDIR/doc" || ! -d "$PKGSRCDIR/mk") {
    print STDERR "Invalid pkgsrc directory $PKGSRCDIR\n";
    exit 1;
}

if (! @PACKAGES) {
    while (<STDIN>) {
        chop;
        push @PACKAGES, $_;
    }
}

foreach $pkg (@PACKAGES) {
    if (! -f "$PKGSRCDIR/$pkg/Makefile") {
        print "$pkg\n";
    } else {
        print "$pkg\n" if revbump("$pkg") == 0;
        $pkg =~ /\/(.*)$/;
    }
}

exit 0;
