dnl
dnl RCSid:
dnl	$Id: configure.in,v 1.6 2007/09/21 13:12:30 rillig Exp $
dnl
dnl Process this file with autoconf to produce a configure script
dnl
AC_INIT(makefile.boot.in)
AC_CONFIG_HEADER(config.h)

dnl
DEFAULT_CSH=/bin/csh
DEFAULT_KSH=/bin/ksh
DEFAULT_SH=/bin/sh
DEFSHELL=1
AC_ARG_WITH(defshell,
[  --with-defshell=SHELL  explicitly set DEFSHELL to either /bin/sh or /bin/ksh],
[case "${withval}" in
yes)   AC_MSG_ERROR(bad value ${withval} given for bmake DEFSHELL) ;;
no)    ;;
*)	case "$with_defshell" in
	*/csh)	DEFSHELL=0; DEFAULT_CSH=$with_defshell;; # kidding right?
	*/sh)  DEFSHELL=1; DEFAULT_SH=$with_defshell;;
	*/ksh) DEFSHELL=2; DEFAULT_KSH=$with_defshell;;
	esac
       ;;
esac])
AC_DEFINE_UNQUOTED(DEFSHELL, ${DEFSHELL}, Shell spec to use by default)
AC_SUBST(DEFAULT_CSH)
AC_SUBST(DEFAULT_KSH)
AC_SUBST(DEFAULT_SH)
dnl
dnl Checks for programs.
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_PROG_INSTALL
dnl Check for OS problems
AC_AIX
AC_MINIX
AC_ISC_POSIX
dnl Executable suffix - normally empty; .exe on os2.
AC_SUBST(ac_exe_suffix)dnl

dnl
dnl AC_C_CROSS
dnl

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_DIRENT
dnl Keep this list sorted
AC_CHECK_HEADERS( \
	ar.h \
	fcntl.h \
	paths.h \
	poll.h \
	ranlib.h \
	string.h \
	sys/select.h \
	sys/socket.h \
	sys/time.h \
	sys/uio.h \
	unistd.h \
	utime.h \
	)

dnl Both *BSD and Linux have sys/cdefs.h, most do not.
dnl If it is missing, we add -I${srcdir}/missing to CFLAGS
dnl also if sys/cdefs.h does not have __RCSID we need to use ours
dnl but we need to include the host's one too *sigh*
AC_CHECK_HEADER(sys/cdefs.h,
echo $ECHO_N "checking whether sys/cdefs.h is compatible... $ECHO_C" >&6
AC_EGREP_CPP(yes,
[#include <sys/cdefs.h>
#ifdef __RCSID
yes
#endif
],
echo yes  >&6,
echo no  >&6; CPPFLAGS="${CPPFLAGS} -I`cd ${srcdir}/missing && pwd` -DNEED_HOST_CDEFS_H"),
CPPFLAGS="${CPPFLAGS} -I`cd ${srcdir}/missing && pwd`")

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C___ATTRIBUTE__
AC_C_BIGENDIAN
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_DECL_SYS_SIGLIST
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VFORK
AC_FUNC_VPRINTF
AC_FUNC_WAIT3
dnl Keep this list sorted
AC_CHECK_FUNCS( \
	asprintf \
	getcwd \
	getenv \
	getopt \
	getwd \
	killpg \
	putenv \
	select \
	setenv \
	sigaction \
	sigvec \
	snprintf \
	strdup \
	strerror \
	strftime \
	strtod \
	strtol \
	vasprintf \
	vsnprintf \
	wait3 \
	wait4 \
	waitpid \
	)
dnl AC_REPLACE_FUNCS(setenv getenv)
AC_REPLACE_FUNCS(getenv)
dnl
dnl Structures
dnl
AC_HEADER_STAT
AC_STRUCT_ST_RDEV
dnl
dnl we want this for unit-tests/Makefile
echo $ECHO_N "checking if diff -u works... $ECHO_C" >&6
if diff -u /dev/null /dev/null > /dev/null 2>&1; then
   diff_u=-u
   echo yes >&6
else
   diff_u=
   echo no >&6
fi
dnl
dnl
dnl Solaris's signal.h only privides sigset_t etc if one of
dnl _EXTENSIONS_ _POSIX_C_SOURCE or _XOPEN_SOURCE are defined.
dnl The later two seem to cause more problems than they solve so if we
dnl see _EXTENSIONS_ we use it.
dnl Note we _don't_ want AC_EGREP_HEADER as we don't want CPP involved.
dnl
AC_EGREP(__EXTENSIONS__, /usr/include/signal.h, 
CPPFLAGS="$CPPFLAGS -D__EXTENSIONS__",)
dnl
dnl AC_* don't quite cut it.
dnl 
echo "checking for MACHINE & MACHINE_ARCH..." >&6
cat > conftest.$ac_ext <<EOF
#include "confdefs.h"
#include <sys/param.h>
#ifdef MACHINE
machine=MACHINE
#endif
#ifdef MACHINE_ARCH
machine_arch=MACHINE_ARCH
#endif
EOF

default_machine=`(eval "$ac_cpp conftest.$ac_ext") 2>&5 | 
	egrep machine= | tr -d ' "'`
rm -rf conftest*
if test "$default_machine"; then
	eval "$default_machine"
fi
machine=${machine:-`$srcdir/machine.sh`}
machine_arch=${machine_arch:-`$srcdir/machine.sh arch`}
echo "defaults: MACHINE=$machine, MACHINE_ARCH=$machine_arch" 1>&6
dnl
dnl now allow overrides
dnl
AC_ARG_WITH(machine,
[  --with-machine=MACHINE  explicitly set MACHINE],
[case "${withval}" in
yes)   AC_MSG_ERROR(bad value ${withval} given for bmake MACHINE) ;;
no)    ;;
generic) machine=`$srcdir/machine.sh`;;
*)     machine=$with_machine;;
esac])
force_machine=
AC_ARG_WITH(force_machine,
[  --with-force-machine=MACHINE  set FORCE_MACHINE],
[case "${withval}" in
yes)   force_machine=FORCE_;;
no)    ;;
*)     force_machine=FORCE_; machine=$with_force_machine;;
esac])
dnl
force_machine_arch=
AC_ARG_WITH(force_machine_arch,
[  --with-force-machine-arch=MACHINE  set FORCE_MACHINE_ARCH],
[case "${withval}" in
yes)   force_machine_arch=FORCE_;;
no)    ;;
*)     force_machine_arch=FORCE_; machine_arch=$with_force_machine;;
esac])
dnl
AC_ARG_WITH(machine_arch,
[  --with-machine_arch=MACHINE_ARCH  explicitly set MACHINE_ARCH],
[case "${withval}" in
yes)   AC_MSG_ERROR(bad value ${withval} given for bmake MACHINE_ARCH) ;;
no)    ;;
*)     machine_arch=$with_machine_arch;;
esac])
dnl
dnl Tell them what we ended up with
dnl
echo "Using: ${force_machine}MACHINE=$machine, MACHINE_ARCH=$machine_arch" 1>&6
dnl
dnl Allow folk to control _PATH_DEFSYSPATH
dnl
DEFAULT_SYS_PATH=\${prefix}/share/mk
AC_ARG_WITH(default-sys-path,
[  --with-default-sys-path=PATH:DIR:LIST  use an explicit DEFAULT_SYS_PATH
	MAKESYSPATH is a ':' separated list of directories 
	that bmake will search for system .mk files. 
	DEFAULT_SYS_PATH is its default value.],
[case "${withval}" in
yes)	AC_MSG_ERROR(bad value ${withval} given for bmake DEFAULT_SYS_PATH) ;;
no)	;;
*)	DEFAULT_SYS_PATH="$with_default_sys_path"
	;;
esac])
AC_SUBST(DEFAULT_SYS_PATH)

dnl
dnl Some folk don't like this one
dnl
AC_ARG_WITH(path-objdirprefix,
[  --with-path-objdirprefix=PATH  override _PATH_OBJDIRPREFIX],
[case "${withval}" in
yes)   AC_MSG_ERROR(bad value ${withval} given for bmake _PATH_OBJDIRPREFIX) ;;
no)    CPPFLAGS="$CPPFLAGS -DNO_PATH_OBJDIRPREFIX" ;;
*)     CPPFLAGS="$CPPFLAGS \"-D_PATH_OBJDIRPREFIX=\\\"$with_path-objdir\\\"\"" ;;
esac])
dnl
dnl And this can be handy to do with out.
dnl
AC_ARG_ENABLE(pwd-override,
[  --disable-pwd-override  disable \$PWD overriding getcwd()],
[case "${enableval}" in
yes)   ;;
no)    CPPFLAGS="$CPPFLAGS -DNO_PWD_OVERRIDE" ;;
*)     AC_MSG_ERROR(bad value ${enableval} given for pwd-override option) ;;
esac])
dnl
dnl Just for grins
dnl
AC_ARG_ENABLE(check-make-chdir,
[  --disable-check-make-chdir disable make trying to guess 
	when it should automatically cd \${.CURDIR}],
[case "${enableval}" in
yes)   ;;
no)    CPPFLAGS="$CPPFLAGS -DNO_CHECK_MAKE_CHDIR" ;;
*)     AC_MSG_ERROR(bad value ${enableval} given for check-make-chdir option) ;;
esac])
dnl
dnl On non-BSD systems, bootstrap won't work without mk
dnl
AC_ARG_WITH(mksrc,
[  --with-mksrc=PATH tell makefile.boot where to find mk src],
[case "${withval}" in
""|yes|no) ;;
*) test -s $withval/install-mk && mksrc=$withval ||
AC_MSG_ERROR(bad value ${withval} given for mksrc cannot find install-mk)
;;
esac
])
dnl
dnl Now make sure we have a value
dnl
for mksrc in $mksrc $srcdir/mk $srcdir/../mk mk
do
        test -s $mksrc/install-mk && break
done
mksrc=`echo $mksrc | sed "s,$srcdir,\\\${srcdir},"`
echo "Using: MKSRC=$mksrc" 1>&6
dnl
AC_SUBST(machine)
AC_SUBST(force_machine)
AC_SUBST(machine_arch)
AC_SUBST(mksrc)
AC_SUBST(INSTALL)
AC_SUBST(GCC)
AC_SUBST(diff_u)
AC_OUTPUT(Makefile makefile.boot lst.lib/makefile.boot unit-tests/Makefile)

cat <<EOF

You can now run

	make -f makefile.boot bootstrap

to produce a fully functional bmake. You can then use:

	make -f makefile.boot install

for an initial installation.  If you already have macros installed 
you can use:

	./bmake -f Makefile

to install etc.

EOF
